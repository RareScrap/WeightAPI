buildscript { // Непонятно почему я не могу разделить этот блок на зависимости для всех проектов и на зависимости для рутпроект. Ругается на невозможность резолва
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url = "https://dl.bintray.com/rarescrap/minecraft"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'ru.rarescrap:depATs:1.0.0' // Для поиска трансформеров в зависимостях
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1' // 1.8.4 не юзается, т.к. он не работает нифига
    }
}

allprojects {
    // TODO: Найти способ подцеплять зависимости из сабпроджектов и при этом не дублировать их
    repositories { // Вообще, этот реп нужен только в case2, но тогда рутпроект не может найти местонахождение configurabeweight
        maven { url = "https://dl.bintray.com/rarescrap/minecraft" }
    }
    apply from: "$rootDir/gradle/mcforge.gradle"
    apply from: "$rootDir/gradle/bintray.gradle"
    group= "ru.rarescrap.weightapi"
    version = "0.5.0_1.7.10"
}

// Предотвращаем зависимость сабпроджектов от самих себя
subprojects { // TODO: Forge дублируется в зависимостях с битым путем
    dependencies {
        compile rootProject
    }
    configurations { // TODO: Должен быть способ лучше эксклюдить кейсы в самих же кейсах. Что-то вроде "transitive = false"
        runtimeOnly.exclude module: 'case1'
        runtimeOnly.exclude module: 'case2'
    }
}

archivesBaseName = "weightapi"
apply plugin: 'DepATs'
minecraft { at(DepATs.getDepATs()) } // Включаем трансформеры из зависимостей
safeClean { exclude += DepATs.getDepATs().name } // Добавляем файл трансформеров-зависимостей в исключение при очистке

dependencies {
    // При запуске игры из рутпроекта сабпроджкты подлючатся как моды
    runtimeOnly project(':case1')
    runtimeOnly project(':case2')
}